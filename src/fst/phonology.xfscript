! There are flags at morpheme boundaries.
! set flag-is-epsilon ON

define Sigma [ a | b | c | d | e | g | h | i | k | m | n | o | s | t | w | y | z ] ;
define Cons [ b | c | d | g | h | k | m | n | s | t | w | y | z ] ;
define Vowel [ a | e | i | o ] ;

! Sometimes there may be more than one consecutive
! morpheme boundary. Reduce those to 1.

define PrefixBoundaryCleanup ["<<"]+ @-> "<<" ;
define SuffixBoundaryCleanup [">>"]+ @-> ">>" ;

define dDeletion d -> 0 || _ ">>" Cons ;

define amRule1 m -> a || a _ ">>" [m|n] ;

define amRule2 m -> n || a _ ">>" [z|g] ;

define nDeletion n -> 0 || [ i | o o ] _ ">>" Cons ;

define vowelDeletion [ Vowel - e ] -> 0 || Cons _ ">>" .#. ;

define wDeletion w -> 0 || _ (">>") .#. ;

define PrefixDInsertion [..] -> d || [ [ n | g ] i | o ] _ "<<"+ Vowel ;
define PrefixOLengthening o -> {oo} || [ [ n | g ] i | o ] d "<<" _ \o ; 

define cleanUp ["<<"|">>"] -> 0 ; 

read regex [ dDeletion .o.
     	     amRule1 .o.
     	     amRule2 .o.
	     vowelDeletion .o.
	     wDeletion .o.
	     nDeletion .o.
	     PrefixDInsertion .o.
	     PrefixOLengthening .o.
	     cleanUp
] ;
