! There are flags at morpheme boundaries.
set flag-is-epsilon ON


! Defining natural classes and morphological boundaries

! All consonants, including special characters
define Cons [ b | c | d | f | g | h | j | k | l | m | n | n1 | p | s | s1 | t | v | w | w1 | w2 | x | y | z | "'" ] ;

! implictly includes long vowels, which just repeats characters
define Vowel [ a | e | i | o | i1 | V1 ] ;

! nonlight vowels are simply short vowels
define NonLight [[Vowel ?* Vowel] | e ] ?* ;

! Define stem boundary markers
define PREBD "<<"+ ;
define SUFBD ">>"+ ;
define BD ["<<" | ">>"]+ ;

! Sometimes there may be more than one consecutive
! morpheme boundary. Reduce those to 1.
define PrefixBoundaryCleanup PREBD @-> "<<" ;
define SuffixBoundaryCleanup SUFBD @-> ">>" ;


! Begin suffix rules

! Begin verb suffix rules

! d-deletion: Delete stem-final "d" 
! when the suffix complex starts with a consonant.
define dDeletion d -> 0 || _ SUFBD Cons ;

! nasalAssimilation: With stems ending in "m", the "m" changes to "n" when the suffix complex starts with "z", "g", or "d".
define nasalAssimilation m -> n || a _ SUFBD [z|g|d] ;

! awaaRule: For stems that end in "aw", the "aw" goes to "aa" when the suffix complex starts with "g" or "k".
define awaaRule w -> a || a _ SUFBD [k|g] ;

! awooRule: For stems that end in "aw", the "aw" goes to "oo" when the suffix complex starts with "n" or "s".
define awooRule a w -> o o || _ SUFBD [n|s] ;

! woRule1, woRule2: For stems that end in "Cw", the "w" goes to "o" 
! when the suffix complex starts with an "i" or "i1". AKA, wi -> o / C __. 
! In Border Lakes, this rule only applies with non-word-final "Cwi" sequences. 
! For example, we get "mizhwi" rather than "mizho" for the imerative form.
define woRule1 [ w -> o1 || Cons _ SUFBD [i|i1] (SUFBD) [? - (SUFBD)] ] .o. [[i|i1] -> 0 || o1 SUFBD _ ] .o. [o1 -> o];
define woRule2 [ w (->) o1 || Cons _ SUFBD [i|i1] (SUFBD) .#. ] .o. [[i|i1] -> 0 || o1 SUFBD _ ] .o. [o1 -> o];

! shared noun and verb suffix rules

! Stems ending in "n1" palatalize to "zh" and "s1" to "sh" 
! when the suffix complex starts with the first person theme sign "i1" 
! or inanimate singular marker "i2".
define n1Rule n1 -> {zh} || _ SUFBD [i1|i2] ;
define s1Rule s1 -> {sh} || _ SUFBD [i1|i2] ;

! Delete word-final short vowels in multi-syllabic words 
! or mono-syllabic words with a long vowel 
! (preseve the short vowel in mono-syllabic words with a short vowel, e.g. "makwa").
define vowelDeletion [ Vowel - e ] -> 0 || NonLight Cons (SUFBD) _ (SUFBD) .#. ;

! w1Deletion: Delete word-final w1
define w1Deletion w1 -> 0 || _ (SUFBD) .#. ;

! LengthenV: a short vowel to the left of v1 lengthens 
! (so i -> ii / _ v1, a -> aa / _ v1, o -> oo / _ v1)
! define LengthenV [ i:{ii} | a:{aa} | {e}:e | o:{oo} @-> || _ SUFBD V1 ] .o. [ V1 -> 0 ] ;
define LengthenV [ i -> {ii} , a -> {aa} , o -> {oo} || _ SUFBD V1 ] .o. [ V1 -> 0 ] ;

! noun-specific suffix rules

! w2Deletion: w2 deletes word finally, before consonants, before vowels "o", 
! before "oo", and before "V1" (so remains before vowels a, aa, i, ii, and e)
define w2Deletion w2 -> 0 || _ (BD) [o | V1 | Cons | .#. ] ; 

! y1hRule: Turn y1 into h if it follows a long vowel + n sequence at the end of a word 
! This handles some stems that are encoded inconsistently; 
! a purely orthographic rule.
define y1hRule y1 -> h || [Vowel Vowel | e] n (SUFBD) _ (SUFBD) .#. ;

! y1Deletion: Delete y1 at the end of a word
define y1Deletion y1 -> 0 || _ (SUFBD) .#. ;

! hDeletion: Optionally delete h between n and y (h -> 0 / n_y). 
! Optional, only because sometimes the spelling convention of deleting that "h" is done inconsistently.
define hDeletion h (->) 0 || n _ y1 ;

! y2Deletion: Always delete y2
define y2Deletion y2 -> 0 ;

! Irregular diminutive form nouns ending in -(i)gan. Appears as -gaans rather than -ganens
define irrDimGan g a n SUFBD e n  -> g a a n ">>" ;

! Irregular diminutive form in VVny stems. For example, "-oon(h)yens" becomes "-oon(h)s" 
define irrDimVVny n y1 SUFBD e n (->) n ">>" || [Vowel Vowel | e] _ ;

! Irregular diminutive form for animosh.
define irrDimAnimosh e n -> o o n || a n i m o s h SUFBD _ ;

! pejorativePalatalization: s and z palatalize to zh when followed by the pejorative marker ish.
define pejorativePalatalization [ s | z ] (->) z h || _ SUFBD i s h ;

! Default rule

! define defaultRules [a1:a | i2:i | n1:n | s1:s | i1:i | w1:w | w2:w | y1:y ] @-> || _ ;
define defaultRules [ a1 -> a , i2 -> i , n1 -> n , s1 -> s , i1 -> i , w1 -> w , w2 -> w , y1 -> y ] ;

! Begin prefix rules

! Sources:
! https://ojibwe.lib.umn.edu/main-entry/g-pf
! https://ojibwe.lib.umn.edu/main-entry/g-pf
! https://ojibwegrammar.langsci.wisc.edu/Assets/Pdfs/InflAnishPersonPrefixes1.02.pdf
!Border Lakes prefix rules:

!ni(m/n/nd)-
!ni- before m, n, w, p, t, k, s, sh
!nim- before b
!nin- before d, j, z, zh, g
!nind- before vowels

!gi(d)-
!gi- before consonants
!gid- before vowels

!o(d)-
!o- before consonants
!od- before vowels

!ni-/n- before m, n, w, p, t, k, s, sh
!nim-/im-/m-/ni- before b
!nin-/in-/n-/ni- before d, j, z, zh, g
!nind-/ind-/nd- before vowels
!At Red Lake, we get niy- before aa

define PrefixDInsertion [..] -> d || .#. (PREBD) [{gi}|o|{ni}] _ PREBD Vowel ;
define PrefixNDInsertion [..] (->) n || .#. (PREBD) {ni} _ d PREBD Vowel ;
define PrefixDDeletion d (->) 0 || .#. (PREBD) {nin} _ PREBD ;
define PrefixIND {nid} (->) [{ind} | {nd}] || .#. (PREBD) _ PREBD;
define PrefixMInsertion [..] (->) m || .#. (PREBD) {ni} _ PREBD b ;
define PrefixNInsertion [..] (->) n || .#. (PREBD) {ni} _ PREBD [d|j|z|{zh}|g] ;
define PrefixNInitialDeletion n (->) 0 || .#. (PREBD) _ [ {in} | {im} ] PREBD ;

define prefixOLengthening o -> {oo} || .#. (PREBD) [ [ [ n | g ] i | o | {nin} ] d | {nin} ] PREBD _ \o ; 

! Dependent noun prefix rules
define DepPrefixIInsertion [..] -> i || .#. (PREBD) [{g}|{n}] _ PREBD Cons ;
define DepPrefixWtoO w -> o || .#. (PREBD) _ PREBD Cons ;
define DepPrefixWDeletion w -> 0 || .#. (PREBD) _ PREBD o ;

! gadaRule
define gadaRule g a "-" -> d a "-" || .#. _ ;

! "dan", "das", "dash", or "daa" (excluding the preverb "daa-"), then add "en-" instead of the usualy initial change ablaut.
! endaso-biboonagizid
define ChCnjEn "<ChCnj>" -> {en} || (PREBD) _ (PREBD) [ {dan} | {das} | {daa} \"-" ] ; 

! Changed conjunct rules
! define ChCnjV {aa}:{ayaa} | a:e | e:{aye} | {ii}:{aa} | i:e | {oo}:{waa} | o:{we} @-> || "<ChCnj>" [? - Vowel]* _ ;
! The weirdness of the rule is on purpose! hfst cannot compile longest match in the way we had above, so we needed to create temporary multicharacter symbols.
define ChCnjV [{aa} -> ayaa , {ii} -> aa, {oo} -> waa || "<ChCnj>" [? - Vowel]* _ ] .o. [ a -> e , e -> {aye}  , i -> e , o -> {we} || "<ChCnj>" [? - Vowel]* _ ] .o. [ ayaa -> {ayaa}, aa -> {aa}, waa -> {waa} ];

define ChCnjDefault "<ChCnj>" -> 0 ;

! Tense-tensing: Lenis consonants ("b", "d", "j", "g", "z", "zh") are strengthened to
! the fortis counterpart ("p", "t", "ch", "k", "s", "sh") following the past tense preverbs
! (gii-/gii'-) and desiderative future tense preverbs (wii-/wii'-).
! define tenseTensing b:p | d:t | j:{ch} | g:k | z:s | {zh}:{sh} @-> || "<T>" "-" (PREBD) _ ;
define tenseTensing [ {zh} -> {sh} || "<T>" "-" (PREBD) _ ] .o. [ b -> p , d -> t , j -> {ch} , g -> k , z -> s || "<T>" "-" (PREBD) _ ] ;


! Allow for deletion of preverb boundaries
define DeletePVHyphen "-" (->) [ 0 | " " ] || _ ?* "<<" ;

! Clean up rules

define emptyFilter [? - "<EMPTYLEX>"]* ;

define cleanUp [ BD | "<T>" ] -> 0 ; 


! Composition, etc. Needs a lot more documentation

!read lexc all.lexc
!define Lexicon

read regex [!emptyFilter .o.
            !cleanUp.i ] .o. Lexicon .o. [ 
						! Suffix Rules
     	   	     	 	n1Rule .o. ! Must precede vowelDeletion
				       	s1Rule .o. ! Must precede vowelDeletion				       
     	   	            nasalAssimilation .o.
				       	awaaRule .o.
				       	awooRule .o.
				       	woRule1 .o. ! Must precede vowelDeletion
				       	woRule2 .o.	! Must precede vowelDeletion			       
			            vowelDeletion .o. ! Must precede w1Deletion, w2Deletion, and y1Deletion; must follow n1Rule, s1Rule, woRule1, and woRule2
			            w1Deletion .o. ! Must precede dDeletion; must follow vowelDeletion
				       	w2Deletion .o. ! Must precede LengthenV; must follow vowelDeletion
                       	dDeletion .o. ! Must follow w1Deletion
						y2Deletion .o.   
                        y1hRule .o. ! Must precede y1Deletion
				       	y1Deletion .o. ! Must precede hDeletion; must follow vowelDeletion and y1hRule
				      	hDeletion .o. ! Must follow y1Deletion
						LengthenV .o. ! Must follow w2Deletion
				       	irrDimVVny .o.
     	   	     	 	irrDimGan .o.
						irrDimAnimosh .o.
						pejorativePalatalization .o.
						! Prefix Rules							       
			           	DepPrefixIInsertion .o.
					   	DepPrefixWtoO .o.
					   	DepPrefixWDeletion .o.
			           	PrefixDInsertion .o.					   
				       	PrefixNDInsertion .o.
				       	PrefixDDeletion .o.
				       	PrefixIND .o.
				       	PrefixMInsertion .o.
				       	PrefixNInsertion .o.
					PrefixNInitialDeletion .o.
			           	prefixOLengthening .o.
					ChCnjEn .o.
				       	ChCnjV .o.
				       	tenseTensing .o.
						gadaRule .o.
						! DeletePVHyphen .o.
						! Cleanup rules
						
				       	defaultRules .o.
				       	ChCnjDefault .o.
				       	emptyFilter ] ;

! Make sure @P.Alt@ flag is on both sides

define Analyzer ;
read regex Analyzer .o. cleanUp ;

! eliminate flags
! save stack ojibwe.fomabin
! write att > ojibwe.att

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! The following is related to Alt-tags which mark alternative
! analyses. We'll comment it out for now.

!set flag-is-epsilon OFF

!define AltRule1 [..] -> "@P.Alt.On@" || .#. "@P.Prefix.NI@" [{ind} PREBD       |
!                                                             {nd}  PREBD       |
!							     {nin} PREBD Vowel ]  ?* _ .#. ;

!read regex Analyzer .o. AltRule1 ;
!twosided flag-diacritics
!define Analyzer
!define AltRule2 [..] -> "+Alt" || _ "@P.Alt.On@" ;
!read regex AltRule2.i .o. Analyzer ;
!define Analyzer

!set flag-is-epsilon ON

!read regex Analyzer .o. cleanUp ;
!define Analyzer ;

!read lexc ojibwe_irregular_verbs.lexc
!define IrregularVerbLexicon ;

!define IrregularLexicon [ IrregularVerbLexicon ] ;

!read regex Analyzer | IrregularLexicon ;

