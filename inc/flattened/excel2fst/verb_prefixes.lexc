!#####
!{Prefixes
!#####

Multichar_Symbols 
@R.Prefix.NONE@
@P.Ccnj.Yes@
@P.Order.Cnj@
@D.Order@
@P.Order.Cnj@
@D.Ccnj@
@R.Ccnj@
PV/CCNJ+
X2
%<%<
%>%>

LEXICON Root
V_PREFIXES ;

LEXICON V_PREFIXES
!1+:ni%<   V_FIRST_PERSON_SET ;
!2+:gi%<   V_SECOND_PERSON_SET ;
!3+:o%<    V_THIRD_PERSON_SET ;
@R.Prefix.NONE@        V_PREVERBS_SUBORDINATOR ;
0          V_PREVERBS_TENSE ;

!_FLAG -> _SET
!LEXICON V_FIRST_PERSON_SET !add _SET to style
!@P.Person.1@  V_PREVERBS_SUBORDINATOR ;
! @P.Person.1@  V_PERSON_EXTENDED ; ! DAB: !extended should only be option for nouns?

!LEXICON V_SECOND_PERSON_SET 
!@P.Person.2@  V_PREVERBS_SUBORDINATOR ;
! @P.Person.2@  V_PERSON_EXTENDED ;

!LEXICON V_THIRD_PERSON_SET 
!@P.Person.3@  V_PREVERBS_SUBORDINATOR ;
! @P.Person.3@  V_PERSON_EXTENDED ;

LEXICON V_PERSON_EXTENDED
!need boundary to stop vowel coalescence across prefix-stem boundary
0:da%*    V_PERSON_EXTENDED_HYPHEN ; 
0:di%*    V_PERSON_EXTENDED_HYPHEN ; 
0:doo%*   V_PERSON_EXTENDED_HYPHEN ; 

LEXICON V_PERSON_EXTENDED_HYPHEN
0:-%<%<  VERBS ;
0:%<%<   VERBS ;

LEXICON V_PREVERBS_SUBORDINATOR
!{ unsure of position
PV/ji+bwaa+:ji-bwaa-%<%<    V_PREVERBS_SUBORDINATOR_ORDER_SET    " 1 before" ; !Rand key:n20114310-3-12n
PV/CCNJ+:PV/CCNJ+   V_PREVERBS_CCNJ_SET ; !changed conjunct trigger (should it precede this slot?)
!}
PV/e+:e-%<%<    V_PREVERBS_CCNJ_SET " 1  <i>historically, the changed form of the preverb <nish>a-</nish>, but its function has broadened in modern usage.</i>"; !This is in competition with initial change. Rhodes 1996:4 indicates that initial change still dominates for participles
PV/a+:a-%<%<   V_PREVERBS_SUBORDINATOR_ORDER_SET " 1 where;  2 when;  3 which, that which"; !does this just syncopate to nothing?
!NO LONGER MUST BE LISTED PV/gaa+:gaa-  V_PREVERBS_SUBORDINATOR_ORDER_SET " 1 the changed form of the past tense preverb <nish>gii-</nish>";
PV/gaa+:gaa-%<%<  V_PREVERBS_SUBORDINATOR_ORDER_SET "that" ;!formerly changed form of gii, has life of own now 
        V_PREVERBS_TENSE ;

LEXICON V_PREVERBS_CCNJ_SET
@P.Ccnj.Yes@    V_PREVERBS_SUBORDINATOR_ORDER_SET ;

LEXICON V_PREVERBS_SUBORDINATOR_ORDER_SET
@P.Order.Cnj@   V_PREVERBS_TENSE ;

LEXICON V_PREVERBS_TENSE
!check Valentine ch 16 for more
!{attention!
PV/daa+gii+:daa-gii-    V_PREVERBS_DIRECTION_BOUND    " 1 would, would have; could; should; can" ; !Rand key:n201012716-46-13n
!}
PV/wii+:wii-   V_PREVERBS_DIRECTION_BOUND " 1 going to [do something], want to [do something], intend [to do something], in order to [do something];  2 future tense, in the future;  3 be in need of (non-living things);  4 please;  5 seeking to [do something];  6 in order to [do something]"; 
PV/gii+:gii-   V_PREVERBS_DIRECTION_BOUND " 1 [past tense]";
PV/gii+:gii-   V_PREVERBS_DIRECTION_BOUND " 1 would, could"; !no changed form
               V_PREVERBS_TENSE_IRREG_MODE_GET ;
               V_PREVERBS_DIRECTION ;

LEXICON V_PREVERBS_TENSE_IRREG_MODE_GET
@D.Order@           V_PREVERBS_TENSE_IRREG_MODE_INDEP ;!use [RD].Order/Ccnj... elsewhere, since there is only one value of order, these are equivalent
@P.Order.Cnj@@D.Ccnj@   V_PREVERBS_TENSE_IRREG_MODE_CNJ ; 
@P.Order.Cnj@@R.Ccnj@   V_PREVERBS_TENSE_IRREG_MODE_CCNJ ;

LEXICON V_PREVERBS_TENSE_IRREG_MODE_INDEP
PV/da+:da-    V_PREVERBS_DIRECTION_BOUND " 1 shall, will; should, could"; 
PV/ga+:ga-    V_PREVERBS_DIRECTION_BOUND " 1 will, shall;  2 let's, let";
PV/daa+:daa-  V_PREVERBS_DIRECTION_BOUND " 1 can, could, ought, should, would"; 

LEXICON V_PREVERBS_TENSE_IRREG_MODE_CNJ
PV/da+:ji-    V_PREVERBS_DIRECTION_BOUND " 1 shall, will; should, could"; 
PV/ga+:ji-    V_PREVERBS_DIRECTION_BOUND " 1 will, shall;  2 let's, let";
PV/daa+:da-   V_PREVERBS_DIRECTION_BOUND " 1 can, could, ought, should, would"; 
PV/daa+:ji-   V_PREVERBS_DIRECTION_BOUND " 1 can, could, ought, should, would"; 

LEXICON V_PREVERBS_TENSE_IRREG_MODE_CCNJ !all of these undergo ccnj -> ge
PV/da+:ga-    V_PREVERBS_DIRECTION_BOUND " 1 shall, will; should, could"; 
PV/ga+:ga-    V_PREVERBS_DIRECTION_BOUND " 1 will, shall;  2 let's, let";
PV/daa+:ga-   V_PREVERBS_DIRECTION_BOUND " 1 can, could, ought, should, would";

LEXICON V_PREVERBS_DIRECTION_BOUND
0:%<%< V_PREVERBS_DIRECTION ;

LEXICON V_PREVERBS_DIRECTION
!{ uncertain of position
PV/zaagji+:zaagiji-    V_PREVERBS_RELATIVE_BOUND    " 1 out of [a place], outside" ; !Rand key:n72972574762n
PV/biindge+:biindige-    V_PREVERBS_RELATIVE_BOUND    " 1 used to indicate action that involves entering a building or room" ; !Rand key:n2010113022-28-53n
PV/bskaabii+:biskaabii-    V_PREVERBS_RELATIVE_BOUND    " 1 go back, return" ; !Rand key:n201132712-36-52n
PV/aazhda+:aazhVv1da-    V_PREVERBS_RELATIVE_BOUND    " 1 from one to another" ; !Rand key:n72974064109n
PV/mbi+:ombi-    V_PREVERBS_RELATIVE_BOUND    " 1 go up [in doing something]" ; !Rand key:n71429448810n
!}
PV/biiji+:biiji-  V_PREVERBS_RELATIVE_BOUND " 1 towards this direction, [towards] this way"; !come in direction
PV/ani+:ani-      V_PREVERBS_RELATIVE_BOUND " 1 [do something] as one goes away, go away and [do something], go away, on one's way out, [doing something];  2 as time goes on, as time passes, in the course of time;  3 in the future";
PV/bimi+:bimi-    V_PREVERBS_RELATIVE_BOUND " 1 come by [and do something], go by [and do something], go by [doing something];  2 in the course of time, as time goes on, in time, through time;  3 going along [doing something], going about [doing something], going about [in the course of one's activities];  4 get around to [doing something]"; !aimless
PV/bibaa+:bibaa-   V_PREVERBS_RELATIVE_BOUND " 1 (moving) around, (going) about in activities, out and about;  2 specifying movement about and around in a specifically imprecise and uncertain sense"; !circumference, exhaustive?
PV/babaa+:babaa-   V_PREVERBS_RELATIVE_BOUND " 1 (moving) around, (going) about in activities, out and about;  2 specifying movement about and around in a specifically imprecise and uncertain sense"; !circumference, exhaustive?
PV/baa+:baa-   V_PREVERBS_RELATIVE_BOUND " 1 around, about, out and about"; !circumference, exhaustive?
PV/awa+:awa-     V_PREVERBS_RELATIVE_BOUND " 1 go and [do sth.]";
PV/oo+:oo-     V_PREVERBS_RELATIVE_BOUND " 1 go [do something], go and [do something]";
PV/wi+:wi-     V_PREVERBS_RELATIVE_BOUND " 1 go and [do something], go [do something]";
                V_PREVERBS_DIRECTION_IRREGULAR_GET ;
            V_PREVERBS_RELATIVE    ;

LEXICON V_PREVERBS_DIRECTION_IRREGULAR_GET
@D.Ccnj@   V_PREVERBS_DIRECTION_IRREGULAR_INDEP ;
@R.Ccnj@   V_PREVERBS_DIRECTION_IRREGULAR_CCNJ ;

LEXICON V_PREVERBS_DIRECTION_IRREGULAR_INDEP
PV/bi+:abi-  V_PREVERBS_RELATIVE_BOUND " 1 come [and do something], come [to do something];  2 in the past"; !come (and _)

LEXICON V_PREVERBS_DIRECTION_IRREGULAR_CCNJ
PV/bi+:abi-  V_PREVERBS_RELATIVE_BOUND " 1 come [and do something], come [to do something];  2 in the past"; !come (and _)
PV/bi+:X2bi-  V_PREVERBS_RELATIVE_BOUND " 1 come [and do something], come [to do something];  2 in the past"; !come (and _)

LEXICON V_PREVERBS_RELATIVE_BOUND
0:%<%< V_PREVERBS_RELATIVE ;

LEXICON V_PREVERBS_RELATIVE
!{unsure of position
PV/bwaa+:bwaa-  V_PREVERBS_RELATIVE_ORDER_SET " 1 lest, not, so that not"; !Valentine 2001 unclear on ordering, while requires conjunct like subordinators, may be preceded by e-, may also be preceded by wii-... semantics definitely not compatible with direction
!}
PV/ako+:ako-  V_PREVERBS_ASPECT_BOUND " 1 since, from such point in time;  2 as far as [a certain point];  3 [for] a long time, as long as";
PV/apiichi+:apiichi-  V_PREVERBS_ASPECT_BOUND " 1 to a certain degree, to a certain extent, to such a degree, to such an extent, how, to what extent, so;  2 in the process of..., while";
PV/izhi+:izhi-  V_PREVERBS_ASPECT_BOUND " 1 as [another], like a certain thing;  2 to a certain place, towards a certain place, in a certain direction;  3 how [to do something], in a certain manner, way [of doing something], thus (in such way), and so;  4 go ahead without preparation, go ahead despite not being ready";
PV/onji+:onji-  V_PREVERBS_ASPECT_BOUND " 1 at a certain place, from a certain place, from a certain point, where;  2 why;  3 have (in the sense of the English perfect tense)";
!wnji-:onji    V_PREVERBS_ASPECT_BOUND    " 1 [see nji-]" ; !Rand key:n73072141838n
PV/dazhi+:dazhi-   V_PREVERBS_ASPECT_BOUND " 1 at a certain place, where";
PV/daso+:daso-     V_PREVERBS_ASPECT_BOUND " 1 each, every"; 
            V_PREVERBS_ASPECT ;

LEXICON V_PREVERBS_RELATIVE_ORDER_SET
@P.Order.Cnj@   V_PREVERBS_ASPECT_BOUND ;

LEXICON V_PREVERBS_ASPECT_BOUND
0:%<%< V_PREVERBS_ASPECT ;

LEXICON V_PREVERBS_ASPECT
!{ uncertain of position
PV/ntami+:nitami-    V_PREVERBS_MANNER_BOUND    " 1 first [before others, i.e., relative position in a sequence]" ; !Rand key:n73099772452n
PV/aanawii+:aanawii-    V_PREVERBS_MANNER_BOUND    " 1 even though, despite;  2 try to..." ; !Rand key:n201012516-47-22n
PV/bgami+:bagami-    V_PREVERBS_MANNER_BOUND    " 1 become, commence, arrive" ; !Rand key:n201132615-13-29n
PV/ekwaa+:ekwaa-    V_PREVERBS_MANNER_BOUND    " 1 finish" ; !Rand key:n73024180454n
PV/endso+:endaso-    V_PREVERBS_MANNER_BOUND    " 1 each, every, each time, every time" ; !Rand key:n73015762439n
PV/epiichi+:epiichi-    V_PREVERBS_MANNER_BOUND    " 1 while" ; !Rand key:n72972557481n
PV/gaagge+:gaagige-    V_PREVERBS_MANNER_BOUND    " 1 forever, for all time" ; !Rand key:n73016041873n
PV/giizhi+:giizhi-    V_PREVERBS_MANNER_BOUND    " 1 finish [doing something]" ; !Rand key:n73024238614n
PV/gji+:goji-    V_PREVERBS_MANNER_BOUND    " 1 try [to do something]" ; !Rand key:n73024559959n
PV/jaagi+:jaagi-    V_PREVERBS_MANNER_BOUND    " 1 all, completely [gone]" ; !Rand key:n73028052357n
PV/megwaaji+:megwaaji-    V_PREVERBS_MANNER_BOUND    " 1 in the middle [of doing something], in the process of [doing something]" ; !Rand key:n71429845986n
PV/mmendaaji+:mamendaaji-    V_PREVERBS_MANNER_BOUND    " 1 want to do more of something" ; !Rand key:n73045650324n
PV/n'kawe+:nakawe-    V_PREVERBS_MANNER_BOUND    " 1 [do something] first [before doing something else], interrupt what one is doing [to do something]" ; !Rand key:n71433050533n
PV/ke+:ke-    V_PREVERBS_MANNER_BOUND    " 1 do something first [before doing something else], do something first [before resuming another activity]" ; !Rand key:n2011448-22-2n
PV/dede+:dede-    V_PREVERBS_MANNER_BOUND    " 1 enough" ; !Rand key:n201132719-28-57n
!}
PV/aazhoo+:aazhoo- V_PREVERBS_MANNER_BOUND " 1 next [period of time]";
PV/booni+:booni-    V_PREVERBS_MANNER_BOUND " 1 quit [doing sth.], stop [doing sth.]";
PV/de+:dX2e-       V_PREVERBS_MANNER_BOUND " 1 fair (amount), moderate (amount)"; !contains blocker to stop dye when ccnj
PV/gagwe+:gagwe- V_PREVERBS_MANNER_BOUND " 1 try to [do something]";
PV/gagweji+:gagweji-   V_PREVERBS_MANNER_BOUND " 1 try to [do something], try and [do something], try [doing something]";
PV/kweji+:gagweji-    V_PREVERBS_MANNER_BOUND    " 1 [see ggweji-]" ; !Rand key:n73032725062n
PV/akawe+:akawe-   V_PREVERBS_MANNER_BOUND " 1 do something first [before doing something else], do something first [before resuming another activity]";
PV/maajii+:maajii-  V_PREVERBS_MANNER_BOUND " 1 begin [to do something], start [to do something]";
PV/nanda+:nanda-  V_PREVERBS_MANNER_BOUND " 1 look [to do something], seek [to do something], in the hope of [something happening]";
PV/nando+:nando-  V_PREVERBS_MANNER_BOUND " 1 [see nda-]";
!PV/nanda+:nda-    V_PREVERBS_MANNER_BOUND    " 1 look [to do something], seek [to do something], in the hope of [something happening]" ; !Rand key:n71431357611n
!PV/nando+:ndo-    V_PREVERBS_MANNER_BOUND    " 1 [see nda-]" ; !Rand key:n71431444116n
PV/noonde+:noonde-  V_PREVERBS_MANNER_BOUND " 1 before a goal is reached, in the attempt of something, before something was to happen, prematurely, unexpectedly";
PV/nitaa+:nitaa-    V_PREVERBS_MANNER_BOUND " 1 be good at [something], do something well;  2 be prone to something, be inclined to do something, [do something] often;  3 know how to do something, be able to do something, can do something";
PV/ishkwaa+:ishkwaa-  V_PREVERBS_MANNER_BOUND " 1 after [doing something];  2 finish [doing something], quit [doing something], end [be over]";
PV/webi+:webi-    V_PREVERBS_MANNER_BOUND " 1 begin [to do something], start [to do something]";
PV/oshki+:oshki-   V_PREVERBS_MANNER_BOUND " 1 first, for the first time, newly";
            V_PREVERBS_MANNER    ;

LEXICON V_PREVERBS_MANNER_BOUND
0:%<%< V_PREVERBS_MANNER ;

LEXICON V_PREVERBS_MANNER
!{ uncertain of position
PV/o+:o-    VERBS_BOUND    " 1 go [do something], go and [do something]" ; !Rand key:n71435144991n
PV/waanji+:waanji-    VERBS_BOUND    " 1 most, to the greatest degree" ; !Rand key:n73034029135n
PV/nshinaaji+:nishinaaji-    VERBS_BOUND    " 1 devastatingly" ; !Rand key:n73099738407n
PV/niibaa+:niibaa-    VERBS_BOUND    " 1 at night" ; !Rand key:n71431746367n
PV/zaami+:ozaami-    VERBS_BOUND    " 1 excessively, overly, too" ; !Rand key:n72977659543n
PV/zaami+:zaami-    VERBS_BOUND    " 1 excessively, overly, too" ; !Rand key:n72977659543n
PV/zhki+:zhaki-    VERBS_BOUND    " 1 damp" ; !Rand key:n73072872654n
PV/npaaji+:nipaaji-    VERBS_BOUND    " 1 mistakenly, incorrectly" ; !Rand key:n201012717-15-45n
PV/mchi+:michi-    VERBS_BOUND    " 1 bare, plain, in the basic and simple manner" ; !Rand key:n20114817-49-43n
PV/ndami+:ondami-    VERBS_BOUND    " 1 be busy with something, be preoccupied with something" ; !Rand key:n72989348365n
PV/nboodewi+:niboodewi-    VERBS_BOUND    " 1 forlorn, lonely" ; !Rand key:n71431348823n
PV/mziwe+:miziwe-    VERBS_BOUND    " 1 all in one unit, in a whole unit, whole" ; !Rand key:n73033330521n
PV/mde+:mVi1de-    VERBS_BOUND    " 1 in the distance" ; !Rand key:n71429470804n
PV/mchiwe+:mVv1chiwe-    VERBS_BOUND    " 1 instead;  2 by hand, from scratch, without mechanical aid" ; !Rand key:n71429457095n
PV/gweki+:gweki-    VERBS_BOUND    " 1 change" ; !Rand key:n73031713691n
PV/gwiini+:gwiini-    VERBS_BOUND    " 1 aimlessly, confusedly, indecisively, unsure of;  2 be unable to think of something [to do]" ; !Rand key:n71429248194n
PV/gnaaj+:gVo1naaj-    VERBS_BOUND    " 1 beautiful, good-looking, pretty, handsome" ; !Rand key:n73029029776n
PV/aakwi+:aakawi-    VERBS_BOUND    " 1 frequently, regularly" ; !Rand key:n72976564856n
PV/aan'ke+:aanike-    VERBS_BOUND    " 1 in succession, in turn" ; !Rand key:n7297341155n
PV/aanji+:aanji-    VERBS_BOUND    " 1 over again" ; !Rand key:n72973362179n
PV/aanooji+:aanooji-    VERBS_BOUND    " 1 dreadfully" ; !Rand key:n73045823524n
PV/baashji+:baashiji-    VERBS_BOUND    " 1 over, to an excessive degree" ; !Rand key:n72915857109n
PV/bgwaji+:bagwaji-    VERBS_BOUND    " 1 [see bgoji-]" ; !Rand key:n72976865281n
PV/bgoji+:bagoji-    VERBS_BOUND    " 1 wild" ; !Rand key:n72976865281n
PV/bzaani+:bizaani-    VERBS_BOUND    " 1 quietly, in the sense of lacking noise, and extended metaphorically in Christian materials to encompass a life lacking turmoil" ; !Rand key:n72985145321n
PV/dami+:ondami-    VERBS_BOUND    " 1 be busy with something, be preoccupied with something" ; !Rand key:n72989341555n
PV/dami+:odami-    VERBS_BOUND    " 1 be busy with something, be preoccupied with something" ; !Rand key:n72989341555n
PV/gchitwaa+:gichitwaa-    VERBS_BOUND    " 1 special, classy, gourmet" ; !Rand key:n73040248510n
!}
PV/niboodwewi+:niboodwewi- VERBS_BOUND " 1 embarassedly" ;
PV/ginibi+:ginibi- VERBS_BOUND " 1 quickly" ;
PV/aana+:aana-   VERBS_BOUND " 1 in vain, to no avail" ; !Rand key:n72973263339n
PV/aano+:aano-   VERBS_BOUND " 1 [see aana-]"; !Valentine 2001 doesn't specify slot
PV/aana+:X2aana-   VERBS_BOUND " 1 in vain, to no avail" ; !optional blocker to stop ccnj yaano!Rand key:n72973263339n
PV/aano+:X2aano-   VERBS_BOUND " 1 [see aana-]"; !optional blocker to stop ccnj yaano!Valentine 2001 doesn't specify slot
PV/aabaji+:aabaji- VERBS_BOUND " 1 constantly, continually, keep [doing something], repeatedly"; !valentine 2001 doesn't mark all etymological vowels in this slot
PV/aapaji+:aapaji- VERBS_BOUND " 1 very;  2 completely, entirely, fully";
PV/baakaji+:baakaji- VERBS_BOUND " 1 irreversible, irreversibly";
PV/bichi+:bichi- VERBS_BOUND " 1 accidentally, by mistake";
PV/bizaani+:bizaani- VERBS_BOUND " 1 quietly, in the sense of lacking noise, and extended metaphorically in Christian materials to encompass a life lacking turmoil";
PV/dago+:dago- VERBS_BOUND " 1 along [with others], among [others], together [with others], with";
PV/gichi+:gichi- VERBS_BOUND " 1 much, lots, extremely, great (in degree), very, hard (in sense of being extreme)";
PV/gagiibaaji+:gagiibaaji- VERBS_BOUND " 1 badly, foolishly";
PV/gagwetaani+:gagwetaani- VERBS_BOUND " 1 exceedingly, extremely, awfully, so, terribly";
PV/gimooji+:gimooji- VERBS_BOUND " 1 covertly, secretly, sneakily, stealthily, on the sly";
PV/giimooji+:giimooji- VERBS_BOUND " 1 covertly, secretly, sneakily, stealthily, on the sly";
PV/maamiiko+:maamiiko- VERBS_BOUND " 1 amazingly, remarkably";
PV/maamwi+:maamwi- VERBS_BOUND " 1 all, all together, among them all";
PV/mamaandaa+:mamaandaa- VERBS_BOUND " 1 amazing, incredible, miraculously";
PV/maandaa+:mamaandaa-   VERBS_BOUND    " 1 [see mmaandaa-]" ; !Rand key:n71429179571n
PV/niizaani+:niizaani- VERBS_BOUND " 1 dangerous";
PV/nipaaj+:nipaaj- VERBS_BOUND " 1 incorrectly, the wrong way";
PV/wiiji+:wiiji- VERBS_BOUND " 1 with";
PV/wiinge+:wiinge- VERBS_BOUND " 1 completely, entirely, thoroughly";
PV/maanaaji+:maanaaji- VERBS_BOUND " 1 bad, evil, harmful, wrong";
PV/maji+:maji- VERBS_BOUND " 1 badly, evilly";
PV/mino+:mino- VERBS_BOUND " 1 good, well;  2 nice, pleasant";
PV/aabita+:aabita- VERBS_BOUND " 1 half, halfway; mid, midway";
PV/nigo+:nigo- VERBS_BOUND " 1 one";
PV/niizho+:niizho- VERBS_BOUND " 1 two"; 
PV/niso+:niso- VERBS_BOUND " 1 three"; 
PV/niiwoo+:niiwoo- VERBS_BOUND " 1 four"; 
PV/niiyoo+:niiyoo- VERBS_BOUND " 1 four" ; 
PV/naano+:naano- VERBS_BOUND " 1 five"; 
PV/nigodwaaso+:nigodwaaso- VERBS_BOUND " 1 six"; 
            VERBS    ;

LEXICON VERBS_BOUND
0:%<%< VERBS ;

LEXICON VERBS
# ;


